
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДоступныеТипы = РеквизитФормыВЗначение("Объект").Метаданные().Реквизиты.ТипРеспондентов.Тип.Типы();
	
	Для каждого ДоступныйТип Из ДоступныеТипы Цикл
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ДоступныйТип);
		Элементы.ТипРеспондентов.СписокВыбора.Добавить(Новый ОписаниеТипов(МассивТипов),Строка(ДоступныйТип));
		
	КонецЦикла;
	
	Если Объект.ТипРеспондентов = Неопределено Тогда
		Если ДоступныеТипы.Количество() > 0 Тогда
			 Объект.ТипРеспондентов = Новый(ДоступныеТипы[0]);
			 ТипРеспондентов = Элементы.ТипРеспондентов.СписокВыбора[0].Значение;
		 КонецЕсли;
	 Иначе
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипЗнч(Объект.ТипРеспондентов));
		ТипРеспондентов = Новый ОписаниеТипов(МассивТипов);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
 	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Печать

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбработатьИзменениеТипаРеспондента();
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если (Объект.ДатаНачала > Объект.ДатаОкончания) И (Объект.ДатаОкончания <> Дата(1,1,1)) Тогда
	
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Нстр("ru = 'Дата начала не может быть больше чем дата окончания.'"),,"Объект.ДатаНачала");
		Отказ = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыборРеспондентов" Тогда
		
		Для каждого ЭлементМассива Из Параметр.ОтобранныеРеспонденты Цикл
			
			Если Объект.Респонденты.НайтиСтроки(Новый Структура("Респондент", ЭлементМассива)).Количество() = 0 Тогда
				
				НоваяСтрока = Объект.Респонденты.Добавить();
				НоваяСтрока.Респондент = ЭлементМассива;
				
			КонецЕсли;
			
		КонецЦикла;
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипРеспондентовПриИзменении(Элемент)
	
	ОбработатьИзменениеТипаРеспондента();
	УправлениеДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура СвободныйОпросПриИзменении(Элемент)
	
	УправлениеДоступностью();
	Если Объект.Респонденты.Количество() > 0 Тогда
		Объект.Респонденты.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ОткрытьФормуРедактированияКомментария(Элемент.ТекстРедактирования, 
		Объект.Комментарий, Модифицированность);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРеспонденты

&НаКлиенте
Процедура РеспондентыРеспондентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Респонденты.ТекущиеДанные;
	
	Значение                 = ТекущиеДанные.Респондент;
	ТекущиеДанные.Респондент = ТипРеспондентов.ПривестиЗначение(Значение);
	Элемент.ВыбиратьТип      = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура РеспондентыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Респонденты.ТекущиеДанные;
	
	Значение                  = ТекущиеДанные.Респондент;
	ТекущиеДанные.Респондент  = ТипРеспондентов.ПривестиЗначение(Значение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

//Открывает форму подбора респондентов
&НаКлиенте
Процедура ПодборРеспондентов(Команда)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ЗакрыватьПртВыборе",Истина);
	СтруктураОтбора.Вставить("РежимВыбора",Истина);
	ОткрытьФорму("Справочник.Пользователи.ФормаВыбора",СтруктураОтбора,Элементы.Респонденты);
	
КонецПроцедуры




#КонецОбласти

#Область СлужебныеПроцедурыИФункцииБСП

#Область ДополнительныеОтчетыИОбработки 

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
  Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
    РезультатВыполнения = Неопределено;
    ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
    ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтотОбъект, РезультатВыполнения);
  КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
  ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

#КонецОбласти

#Область Печать
 
// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обрабатывает изменение типа респондента.
&НаКлиенте
Процедура ОбработатьИзменениеТипаРеспондента()
	
	Элементы.РеспондентыРеспондент.ОграничениеТипа  = ТипРеспондентов;
	Элементы.РеспондентыРеспондент.ДоступныеТипы	= ТипРеспондентов;
	
	Если Объект.ТипРеспондентов <> Неопределено Тогда
		Объект.ТипРеспондентов = Новый(ТипРеспондентов.Типы()[0]);
	КонецЕсли;
	
	Для каждого СтрокаРеспонденты Из Объект.Респонденты Цикл
		
		Если Не ТипРеспондентов.СодержитТип(ТипЗнч(СтрокаРеспонденты.Респондент)) Тогда
			Объект.Респонденты.Очистить();
			Элементы.Респонденты.Обновить();
		КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры

// Управляет доступностью элементов формы
&НаКлиенте
Процедура УправлениеДоступностью()

	//Элементы.Респонденты.ТолькоПросмотр           = Объект.СвободныйОпрос;
	//Элементы.КнопкаПодборРеспондентов.Доступность = НЕ Объект.СвободныйОпрос;
	Элементы.КнопкаПодборРеспондентов.Доступность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РеспондентыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		НоваяСтрока = Объект.Респонденты.Добавить();
        НоваяСтрока.Респондент = ВыбранноеЗначение;
		
	КонецЕсли;		
	
КонецПроцедуры


#КонецОбласти


